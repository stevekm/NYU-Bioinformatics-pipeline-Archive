#!/bin/tcsh
source ./code/code.main/custom-tcshrc      # customize shell environment

##
## USAGE: pipeline-index 
##

if ($#argv != 0) then
  grep '^##' $0
  exit
endif

set prefix = __
rm -f ${prefix}*
set f = pipeline/index.txt
set n = `cat $f | wc -l`
set k = 1
set head1 = 1
set head2 = 97   # ASCII code for 'a'
while ($k <= $n)
  set inp = `cat $f | tools-rows -1 $k`
  if ($inp != '') then
    if (`echo $inp | cut -c1` == '#') goto skip
    foreach folder ($inp)
      set head1str = `awk -v i=$head1 'BEGIN{printf "%02d",i}'`
      set head2str = `awk -v i=$head2 'BEGIN{printf "%c",i}'`
      set out = __${head1str}${head2str}-$folder
      echo "$out <- $folder"
      if (! -e $out) then
        ln -s pipeline/$folder $out
      else
        scripts-send2err "Warning: symbolic link $out already exists, skipping..."
      endif  
      @ head2 ++
    end
  else
    @ head1 ++
    set head2 = 97
  endif
skip:
  @ k ++
end

