#!/bin/tcsh
source ./code/code.main/custom-tcshrc         # customize shell environment

##
## USAGE: pipeline-install-step OUT-DIRECTORY INP-DIRECTORIES PARAM-SCRIPTS WRAPPER-SCRIPTS
##

if ($#argv != 4) then
  grep '^##' $0
  exit
endif

set outdir = $1
set inpdirs = ($2)
set params = ($3)
set wrappers = ($4)

if (-e $outdir) then
  scripts-send2err "Error: specified output directory already exists."
  exit 1
endif

mkdir $outdir
cd $outdir

ln -s ../inputs
ln -s ../code
ln -s code/clean.tcsh
ln -s code/run-$outdir.sh run
foreach d ($inpdirs)
  ln -s ../$d
end

mkdir params
cd ..

cp $params $outdir/params/
cp $wrappers $outdir/code/

set inpresults = `echo $inpdirs | tr ' ' '\n' | sed 's/$/\\\/results/'`
cat code/run-template.sh | sed "s/%%op%%/$outdir/g" | sed 's/%%threads%%/1/g' | sed 's/%%method%%/by-branch/' | sed "s/%%inpresults%%/$inpresults/g" >! code/run-$outdir.sh
chmod +x code/run-$outdir.sh





