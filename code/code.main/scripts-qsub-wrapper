#!/bin/tcsh
#$ -S /bin/tcsh

##
## USAGE: scripts-qsub-wrapper THREADS[,MEMORY] SCRIPT-PATH OUTPUT-DIR ARGS ...
##

if ($#argv < 3) then
  grep '^##' $0
  exit
endif

set resources = $1
set prog = `which $2`
set out = $3
shift
shift

set nthreads = `echo $resources, | cut -d',' -f1`
set mem = `echo $resources, | cut -d',' -f2`

if (! -e $out) then
  set uptodate = false
else
#  find $out -type f -printf '%T+ %p\n' | sort | head -n 1               # TODO: need to compare date OUTPUT-DIR was created to the most recent date in SCRIPT-PATH and ARGS
  set uptodate = true
endif

if ($uptodate == false) then
  scripts-send2err "[scripts-qsub-wrapper] Generating results in directory \'$out\'..."
  if (-e $out) rm -rf $out
  scripts-create-path $out/ 
  set pref = $out/job
  scripts-print-cmdline $prog $argv:q >! $pref.sh
  if ("$mem" == "") then
    qsub -cwd -o :$pref.out -e :$pref.err -pe threaded $nthreads ./$pref.sh >! $pref.id
  else
    qsub -cwd -l mem_free=$mem -l h_vmem=$mem -l mem_token=$mem -o :$pref.out -e :$pref.err -pe threaded $nthreads ./$pref.sh >! $pref.id
  endif  
  cat $pref.id | cut -d' ' -f3                                                                 # TODO: need a general method to obtain job id
else 
  scripts-send2err "[scripts-qsub-wrapper] Directory \'$out\' exists, skipping..."             # TODO: change this message to up-to-date when implemented
endif







